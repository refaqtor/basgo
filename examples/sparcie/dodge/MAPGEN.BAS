10 REM map generator for text dodge
11 OPTION BASE 1
15 DEFINT A-Z
20 DIM MAP%(75,22)
30 RANDOMIZE VAL(RIGHT$(TIME$,2))*VAL(LEFT$(TIME$,2))
40 FOR I= 1 TO 75
50 FOR C= 1 TO 22
60 MAP(I,C)=0
70 NEXT C
80 NEXT I
90 REM place the platforms!
100 COUNT = INT(RND*30)+4
110 FOR I= 1 TO COUNT
120 X = INT(RND*75)+1
130 Y = INT(RND*21)+1
140 W=INT(RND*10)+10
150 FOR C= 1 TO W
160 MAP(X,Y)=1
170 IF X<75 THEN X=X+1
180 NEXT C
190 NEXT I
200 REM place some ladders
210 COUNT = INT(RND*7)+1
220 FOR I= 1 TO COUNT
230 X = INT(RND*75)+1
240 Y = INT(RND*22)+1
250 MAP(X,Y)=2
260 IF Y=22 THEN GOTO 300
270 IF MAP(X,Y+1)=1 THEN GOTO 300
280 Y=Y+1
290 GOTO 250
300 MAP(X,Y)=2
310 IF Y=1 THEN GOTO 350
320 IF MAP(X,Y-1)=1 THEN MAP(X,Y-1)=2: GOTO 350
330 Y=Y-1
340 GOTO 300
350 NEXT I
360 REM add the spikes!
370 COUNT = INT(RND*5)+1
380 FOR I = 1 TO COUNT
390 X = INT(RND*75)+1
400 Y = INT(RND*22)+1
410 IF Y=22 THEN GOTO 450
420 IF MAP(X,Y+1)=1 THEN GOTO 450
430 Y=Y+1
440 GOTO 410
450 MAP(X,Y)=3
460 NEXT I
470 REM convert a platform to a conveyor
480 COUNT = INT(RND*40)
490 IF COUNT >10 THEN GOTO 600
500 X = INT(RND*75)+1
510 Y = INT(RND*22)+1
520 IF MAP(X,Y)<>1 THEN GOTO 500
530 COUNT = INT(RND*40)
540 W=4
550 IF COUNT>20 THEN W=5
560 MAP(X,Y)=W
570 IF X=75 THEN GOTO 600
580 IF MAP(X+1,Y)<>1 THEN GOTO 600
590 X=X+1: GOTO 560
600 REM save an ascii file with this stored in it!
610 OPEN "autogen.txt" FOR OUTPUT AS #1
620 FOR C= 1 TO 22
630 FOR I = 1 TO 75
640 O$ = " "
650 IF MAP(I,C) = 1 THEN O$="O"
660 IF MAP(I,C) = 2 THEN O$="#"
670 IF MAP(I,C) = 3 THEN O$="^"
680 IF MAP(I,C) = 4 THEN O$="<"
690 IF MAP(I,C) = 5 THEN O$=">"
700 PRINT #1,O$;
710 NEXT I
715 PRINT #1, "E"
720 NEXT C
730 CLOSE #1
10000 REM draw the map
10010 FOR I= 1 TO 75
10020 FOR C= 1 TO 22
10030 LOCATE C,I
10040 IF MAP(I,C)=0 THEN COLOR 7,0:PRINT " ";
10050 IF MAP(I,C)=1 THEN COLOR 13,0:PRINT CHR$(177);
10060 IF MAP(I,C)=2 THEN COLOR 7,0:PRINT CHR$(197);
10070 IF MAP(I,C)=3 THEN COLOR 3,0:PRINT CHR$(127);
10080 IF MAP(I,C)=4 THEN COLOR 0,13:PRINT CHR$(174);
10090 IF MAP(I,C)=5 THEN COLOR 0,13:PRINT CHR$(175);
10100 NEXT C
10110 NEXT I
