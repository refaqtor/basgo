1 OPTION BASE 1
10 DIM MAP(75,22)
20 LINENUMBER = 10
30 PRINT "Dodge map conerter"
40 PRINT "converts txt files to RLE data segments for the end of dodge"
50 INPUT "Line Number to start at";LINENUMBER
60 INPUT "output text file";O$
65 OPEN O$ FOR OUTPUT AS #2
70 INPUT "text file to add";F$
75 S$ = STR$(LINENUMBER) + " REM " + F$ : PRINT #2, S$ : LINENUMBER=LINENUMBER+5
80 GOSUB 10000
90 FOR C = 1 TO 22
91 PRINT ".";
100 CC = 1 : CI = MAP(1,C)
105 S$ = STR$(LINENUMBER) + " DATA"
110 FOR I = 1 TO 74
130 IF MAP(I+1,C)=CI THEN CC=CC+1 : GOTO 160
140 S$ = S$ + STR$(CC) +"," + STR$(CI)  + ","
150 CC = 1 : CI = MAP(I+1,C)
160 NEXT I
170 S$ = S$ + STR$(CC) +"," + STR$(CI)
180 PRINT #2,S$
190 LINENUMBER = LINENUMBER + 5
200 NEXT C
201 PRINT " "
210 PRINT "Add another level? (y/n)"
220 C$ = INPUT$(1)
230 IF C$="y" THEN 70
240 IF C$="n" THEN CLOSE #2 : END
250 GOTO 220
10000 OPEN F$ FOR INPUT AS #1
10010 I=1: C=1
10020 C$ = INPUT$(1,#1)
10025 IF I>75 THEN 10090
10030 IF C$ = "O" THEN MAP(I,C)=1
10040 IF C$ = "#" THEN MAP(I,C)=2
10050 IF C$ = "^" THEN MAP(I,C)=3
10051 IF C$="v" THEN MAP(I,C)=4
10060 IF C$ = "<" THEN MAP(I,C)=5
10070 IF C$ = ">" THEN MAP(I,C)=6
10080 IF C$ = " " THEN MAP(I,C)=0
10081 I=I+1
10090 IF C$ = CHR$(13) THEN I=0:C=C+1
10100 IF C<23 AND NOT(EOF(1)) THEN 10020
10110 CLOSE #1
10120 RETURN
