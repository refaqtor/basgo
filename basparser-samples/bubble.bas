100 DATA "HELLO", "WORLD", "BASIC", "BUBBLE", "SORT", "DEMO", "GO"
110 LET J=7
120 FOR N=1 TO J
130 READ A$(N)
140 NEXT
150 PRINT "SORTING ARRAY"
160 GOSUB 1000
170 PRINT "SORTING ARRAY - DONE"
180 PRINT
190 FOR N=1 TO J
200 PRINT A$(N)
210 NEXT
220 END
1000 REM BUBBLE SORT ARRAY
1010 REM INPUT: A$()=ARRAY J=ARRAY_SIZE
1020 REM OUTPUT: A$()=SORTED_ARRAY
1030 REM OTHER VARIABLES USED: FLIPS, N
1040 REM
1050 LET FLIPS=1
1060 WHILE FLIPS
1070 LET FLIPS=0
1080 FOR N=1 TO J-1
1090 IF A$(N)>A$(N+1) THEN SWAP A$(N), A$(N+1): LET FLIPS=1
1100 NEXT N
1110 WEND
1120 RETURN
